class GSW_ShoulderStickyLauncher : CustomInventory replaces RocketLauncher
{
	const STICKYLAYER = -100;
	override void AttachToOwner(Actor other)
	{
		super.AttachToOwner(other);
		if (!other || !other.player)
		{
			return;
		}
		let psp = other.player.FindPSprite(STICKYLAYER);
		if (!psp)
		{
			psp = other.player.GetPSprite(STICKYLAYER);
			psp.caller = self;
			psp.bAddBob = false;
			psp.bAddWeapon = false;
			psp.bPlayerTranslated = true;
			psp.SetState(ResolveState("ReadySticky"));
		}
	}
	override bool TryPickup(in out Actor toucher)
	{
		toucher.A_GiveInventory("GSW_StickyAmmo", 2);
		return super.TryPickup(toucher);
	}
	override void Travelled()
	{
		super.Travelled();
		AttachToOwner(owner); // very dubious fix, i will not be surprised if something breaks later
	}
	Default
	{
		+INVENTORY.ALWAYSPICKUP // so you can get sticky ammo out of it
		+INVENTORY.UNDROPPABLE
		+INVENTORY.UNTOSSABLE
		+INVENTORY.PERSISTENTPOWER
		+INVENTORY.AUTOACTIVATE //Use is called automatically
		Inventory.PickupMessage "$GOTSTICKYLAUNCHER";
	}
	States 
	{
		Spawn:
			STKI Z -1;
			Stop;
		Use:
			TNT1 A 0 
			{
				// We're passing 'true' to the last argument, so
				// that the layer doesn't get overridden if for
				// some reason the item is used again:
				A_Overlay(STICKYLAYER, "ReadySticky", true);
			}
			fail;
		ReadySticky:
			TNT1 A 1
			{
				// Check if +user2 is pressed:
				if (player.health > 0 && player.cmd.buttons & BT_USER2 && invoker.owner.CountInv("GSW_StickyAmmo") > 0)
				{
					// move to DoKick if so:
					return ResolveState("DoSticky");
				}
				// Otherwise loop this layer
				return ResolveState(null);
			}
			loop;
		DoSticky:
			TNT1 A 0 A_OverlayOffset(OverlayID(), -20, WEAPONTOP);
			STKI ABCDEFF 2;
		ReFireSticky:
			STKI F 2 {
				if (invoker.owner.CountInv("GSW_StickyAmmo") > 0)
				{
					A_Overlay(STICKYLAYER - 1, "FlashSticky");
					A_OverlayOffset(STICKYLAYER - 1, -20, WEAPONTOP);
				}
			}
			STKI G 3 {
				if (invoker.owner.TakeInventory("GSW_StickyAmmo", 1, true, true) == true)
				{
					A_FireProjectile("GSW_ShoulderSticky", -3.5, false, -20, 7, FPF_TRANSFERTRANSLATION);
					A_StartSound("weapons/stickyshot");
					return ResolveState(null);
				}
				else
				{
					return ResolveState("FinishSticky");
				}
			}
			STKI F 12;
			STKI E 0
			{
				if (player.cmd.buttons & BT_USER2 && player.oldbuttons & BT_USER2) // do an ammo check later
				{
					return ResolveState("ReFireSticky");
				}
				return ResolveState(null);
			}
		FinishSticky:
			STKI EDCBA 2;
			Goto ReadySticky; //Note that we go back to ready from here
		FlashSticky:
			STKF ABCCDD 1;
			Stop;
	}
}
class GSW_ShoulderSticky : Actor
{
	int user_missiletime; //Timer for the missile to explode or drop.
	Default
	{
		Projectile;
		Radius 4;
		Height 4;
		Speed 25;
		Damage 3;
		Gravity 0.5;
		-NOGRAVITY
		+HITTRACER //To allow the missile to stick on an actor
		Obituary "$KILLSTICKYLAUNCHER";
	}
	States
	{
		Spawn:
			STKP A 1 Bright A_SpawnItemEx("GSW_StickyTrail", flags: SXF_TRANSFERTRANSLATION);
			Loop;
		Crash:
		Death:
			TNT1 A 0 {bNOGRAVITY = true;}
			STKP A 1 BRIGHT {user_missiletime += 1;}
			STKP A 0 A_JumpIf(user_missiletime >= 35,"CrashDeath.LowTime");
			Loop;
		CrashDeath.LowTime:
			STKP B 0 A_PlaySound("weapons/stickybeep");
			STKP B 1 BRIGHT {user_missiletime += 1;}
			STKP B 0 A_JumpIf(user_missiletime >= 70,"Explode");
			STKP C 1 BRIGHT {user_missiletime += 1;}
			STKP C 0 A_JumpIf(user_missiletime >= 70,"Explode");
			STKP D 1 BRIGHT {user_missiletime += 1;}
			STKP D 0 A_JumpIf(user_missiletime >= 70,"Explode");
			Loop;
		XDeath:
			TNT1 A 0 {
				A_Stop();
				bSOLID = true;
			}
		StickOnEnemy:
			STKP A 0 {user_missiletime += 1;}
			STKP A 0 A_JumpIf(user_missiletime >= 35,"StickOnEnemy.LowTime");
			STKP A 0 A_JumpIfHealthLower(1, "DropDown", AAPTR_TRACER);
			STKP A 1 BRIGHT A_Warp(AAPTR_TRACER, 0, 0, 20, 0, WARPF_NOCHECKPOSITION);
			Loop;
		StickOnEnemy.LowTime:
			STKP B 0 {user_missiletime += 1;}
			STKP B 0 A_JumpIf(user_missiletime >= 70,"Explode");
			STKP B 0 A_JumpIfHealthLower(1, "DropDown", AAPTR_TRACER);
			STKP B 0 A_PlaySound("weapons/stickybeep");
			STKP B 1 BRIGHT A_Warp(AAPTR_TRACER, 0, 0, 20, 0, WARPF_NOCHECKPOSITION);
			STKP C 0 {user_missiletime += 1;}
			STKP C 0 A_JumpIf(user_missiletime >= 70,"Explode");
			STKP C 0 A_JumpIfHealthLower(1, "DropDown", AAPTR_TRACER);
			STKP C 1 BRIGHT A_Warp(AAPTR_TRACER, 0, 0, 20, 0, WARPF_NOCHECKPOSITION);
			STKP D 0 {user_missiletime += 1;}
			STKP D 0 A_JumpIf(user_missiletime >= 70,"Explode");
			STKP D 0 A_JumpIfHealthLower(1, "DropDown", AAPTR_TRACER);
			STKP D 1 BRIGHT A_Warp(AAPTR_TRACER, 0, 0, 20, 0, WARPF_NOCHECKPOSITION);
			Loop;
		DropDown:
			STKP A 0 A_Gravity;
			Goto Death;
		Explode:
			TNT1 A 0 {
				A_Explode();
				A_StartSound("effects/explosion_small");
				A_SpawnItemEx("GSW_Explosion");
			}
			Stop;
	}
}
class GSW_StickyTrail : Actor
{
	Default
	{
		+NOTELEPORT
		+NOINTERACTION
		Renderstyle "Add";
		Scale 1;
		Alpha 0.75;
	}
	States
	{
		Spawn:
			STKP E 2 Bright A_Fadeout(0.075);
			Loop;
	}
}