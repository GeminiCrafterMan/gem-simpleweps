// Glock
// An infinite-ammo pistol that requires reloading.
// Holds 17 shots in the mag, and one more in the chamber. Reflect this in the reload from empty!
class GSW_GlockAmmo : Ammo
{
	Default
	{
		Inventory.Icon "GLOCZ0";
		Inventory.Amount 0;
		Inventory.MaxAmount 18;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 18;
		+INVENTORY.IGNORESKILL
	}
}
class GSW_Glock : GSW_Weapon replaces Pistol
{
	Default
	{
		+WEAPON.AMMO_OPTIONAL
		Weapon.SlotNumber 2;
		Weapon.AmmoType "GSW_GlockAmmo";
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 0;

		Tag "$TAG_GLOCK";
		Obituary "$KILLGLOCK";
		Inventory.PickupMessage "$GOTGLOCK";

		AttackSound "weapons/glock/fire";
		Weapon.UpSound "weapons/glock/draw";

		DamageType "Bullet";
	}
	States
	{
		Spawn:
			GLOC Z -1;
			Stop;
		Select:
			GLOC A 1 A_Raise(18);
			Loop;
		Deselect:
			GLOC A 1 A_Lower(18);
			Loop;
		Ready:
			GLOC A 1 A_WeaponReady(WRF_ALLOWRELOAD);
			Loop;
		Fire:
			GLOC A 0 A_JumpIfNoAmmo("FireEmpty");
			GLOC A 0 {
				// heeere we gooooooo hitscan stuff and yeah
				A_FireBullets(2, 2, -1, 12, flags: FBF_USEAMMO|FBF_NORANDOM|FBF_NORANDOMPUFFZ, missile: "GSW_BulletTracer", -2.5, 7.5); // -1 bullets to always fire with spread
				A_Overlay(-2, "Flash");
			}
			GLOC BC 2;
			GLOC CCDDDDAAAA 1 A_JumpIf(JustPressed(BT_ATTACK), "Fire");
			// Go to "Empty" reload state if nothing's in the mag
			GLOC AAAA 1 A_JumpIf(JustPressed(BT_ATTACK), "Fire");
			Goto Ready;
		FireEmpty:
			GLOC A 0 A_StartSound("weapons/glock/empty");
			GLOC BCCDDEE 2;
			Goto ReloadEmpty;
		ReloadEmpty: // Reload, but only 17 bullets.
		// Not bothering to check for any ammo type.
			GLOC FGHI 2;
			GLOC J 6 A_StartSound("weapons/glock/clipout");
			GLOC K 10;
			GLOC L 4;
			GLOC M 3 A_StartSound("weapons/glock/clipin");
		ReloadEmpty.Repeat:
			// if 17 bullets, finish reload
			TNT1 A 0 A_JumpIfInventory("GSW_GlockAmmo", invoker.ammo1.MaxAmount - 1, "ReloadEmpty.Finish");
			TNT1 A 0 A_GiveInventory("GSW_GlockAmmo", 1);
			Goto ReloadEmpty.Repeat;
		ReloadEmpty.Finish:
			GLOC N 3;
			GLOC I 5;
			GLOC O 4 A_StartSound("weapons/glock/sliderelease");
			GLOC PQR 2;
			Goto Ready;

		Reload: // Reload, with the full 18.
		// Not bothering to check for any ammo type, but do check if you're already full.
			TNT1 A 0 A_JumpIfInventory("GSW_GlockAmmo", 0, "Ready"); // Aughhh. I would totally make an "inspect" animation, but that'd cost more frames.
			TNT1 A 0 A_JumpIfNoAmmo("ReloadEmpty");
			GLOC RQST 2;
			GLOC U 6 A_StartSound("weapons/glock/clipout");
			GLOC V 10;
			GLOC W 4;
			GLOC X 3 A_StartSound("weapons/glock/clipin");
		Reload.Repeat:
			// if full on ammo, finish reload
			TNT1 A 0 A_JumpIfInventory("GSW_GlockAmmo", 0, "Reload.Finish");
			TNT1 A 0 A_GiveInventory("GSW_GlockAmmo", 1);
			Goto Reload.Repeat;
		Reload.Finish:
			GLOC Y 3;
			GLOC T 5;
			GLOC PQR 2;
			Goto Ready;
		Flash:
			GLOF A 1 Bright A_AttachLightDef("Flash", "GSW_GunFlash");
			GLOF B 1 Bright;
			GLOF B 0 A_RemoveLight("Flash");
			Stop;
	}
}