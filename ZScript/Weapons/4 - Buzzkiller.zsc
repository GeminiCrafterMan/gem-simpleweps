// "Buzzkiller" Machine Gun
// It's a machine gun. Dakkadakkadakka.
// No mag. Reloading a machine gun is boring!!
class GSW_Buzzkiller : GSW_Weapon replaces Chaingun
{
	Default
	{
		+WEAPON.AMMO_OPTIONAL
		Weapon.SlotNumber 4;
		Weapon.AmmoType "GSW_BulletMag";
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 20;

		Tag "$TAG_BUZZKILLER";
		Obituary "$KILLBUZZKILLER";
		Inventory.PickupMessage "$GOTBUZZKILLER";

		AttackSound "weapons/buzzkiller/fire";
		Weapon.UpSound "weapons/buzzkiller/draw";
		Inventory.PickupSound "weapons/buzzkiller/pickup";
		
		Decal "BulletChip";
		DamageType "Chaingun"; // Supercharge compatibility
	}
	States
	{
		Spawn:
			BUZG Z -1;
			Stop;
		Select:
			BUZG A 1 A_Raise(18);
			Loop;
		Deselect:
			BUZG A 1 A_Lower(18);
			Loop;
		Ready:
			BUZG A 1 A_WeaponReady;
			Loop;
		Fire:
			BUZG A 0 A_JumpIfNoAmmo("FireEmpty");
			BUZG A 2 {
				A_FireBullets(3, 3, -1, 15, flags: FBF_USEAMMO|FBF_NORANDOM|FBF_NORANDOMPUFFZ, missile: "GSW_BulletTracer", -2.5, 7.5);
				A_Overlay(-2, "Flash");
			}
			BUZG BC 1;
			BUZG A 2 A_ReFire;
			Goto Ready;
		FireEmpty:
			BUZG A 10 A_StartSound("weapons/buzzkiller/empty");
			Goto Ready;
		Flash:
			BUZF A 1 Bright A_AttachLightDef("Flash", "GSW_GunFlash");
			BUZF B 1 Bright;
			BUZF B 0 A_RemoveLight("Flash");
			Stop;
	}
}