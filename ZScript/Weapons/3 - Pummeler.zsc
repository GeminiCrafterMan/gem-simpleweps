// "Pummeler" Pump Shotgun
// A high-power projectile shotgun that takes quite a bit of effort to pump, made easier with berserk.
// I don't know if I want to give this thing a mag...
class GSW_Pummeler : GSW_Weapon replaces Shotgun
{
	Default
	{
		Weapon.SlotNumber 3;
		Weapon.AmmoType "Shell";
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 4;

		Tag "$TAG_PUMMELER";
		Obituary "$KILLPUMMELER";
		Inventory.PickupMessage "$GOTPUMMELER";

		AttackSound "weapons/pummeler/fire";
		
		Decal "BulletChip";
	}
	States
	{
		Spawn:
			PUMG Z -1;
			Stop;
		Select:
			PUMG A 1 A_Raise(18);
			Loop;
		Deselect:
			PUMG A 1 A_Lower(18);
			Loop;
		Ready:
			PUMG A 1 A_WeaponReady;
			Loop;
		Fire:
			PUMG A 2 {
				// heeere we gooooooo hitscan stuff and yeah
				A_FireBullets(4, 4, 10, 12, flags: FBF_USEAMMO|FBF_NORANDOM|FBF_NORANDOMPUFFZ); // -1 bullets to always fire with spread, and do stuff with missile params when you add tracers
				A_GunFlash();
			}
			PUMG A 0 A_JumpIfInventory("PowerStrength", 1, "Pump.Berserk");
			PUMG BCDEFGH 2; // Recoil
			PUMG I 2; // Pump
			PUMG J 2 A_StartSound("weapons/pummeler/pumpin");
			PUMG K 5;
			PUMG L 3 A_StartSound("weapons/pummeler/pumpout");
			PUMG MN 3;
			PUMG A 4;
			PUMG A 4 A_ReFire;
			Goto Ready;
		Pump.Berserk:
			PUMG H 2;
			PUMG I 2; // Pump
			PUMG J 2 A_StartSound("weapons/pummeler/pumpin");
			PUMG K 3;
			PUMG L 2 A_StartSound("weapons/pummeler/pumpout");
			PUMG MN 3;
			PUMG A 4;
			PUMG A 4 A_ReFire;
			Goto Ready;
		Flash:
			PUMF A 1 Bright A_AttachLightDef("Flash", "GSW_GunFlash");
			PUMF B 1 Bright;
			PUMF B 0 A_RemoveLight("Flash");
			Stop;
	}
}