// Heavy Anti-Material Rifle
// Doesn't require reloading, deals enough damage to one-hit a Revenant.
// "I too use a fucking sniper rifle for CQC."
// "Every situation I see is a nail, and the M82 I'm holding is a hammer."
// "That thunderous CRACK of a .50 BMG every time you pull the trigger makes it feel like you're PUNCHING a hole in the UNIVERSE."
class GSW_HeavyAMR : GSW_Weapon replaces PlasmaRifle
{
	Default
	{
		+WEAPON.AMMO_OPTIONAL
		Weapon.Kickback 400;
		Weapon.SlotNumber 5;
		Weapon.AmmoType "GSW_20mmMag";
		Weapon.AmmoGive 40;
		Weapon.AmmoUse 10;

		Tag "$TAG_HEAVYAMR";
		Inventory.PickupMessage "$GOTHEAVYAMR";

		Weapon.UpSound "weapons/heavyamr/draw";
		Inventory.PickupSound "weapons/heavyweps/pickup";

		Obituary "$KILLHEAVYAMR_MELEE";
	}
	States
	{
		Spawn:
			HAMR Z -1;
			Stop;
		Select:
			HAMR A 1 {
				A_Raise(12);
				A_Overlay(-3, "Hand");
			}
			Loop;
		Deselect:
			HAMR AAAAAA 1 {
				A_Lower(12);
				A_StopSound(CHAN_BODY);
			}
			HAMR A 0 A_Overlay(-3, "HandStop");
			Loop;
		Ready:
			HAMR A 1 A_WeaponReady;
			Loop;
		Fire:
			HAMR A 0 A_JumpIfNoAmmo("Fire.Empty");
			HAMR A 1 {
				A_Overlay(-2, "Flash");
				A_FireProjectile("GSW_HeavyAMRBullet", 0, true, 7.5, -2.5);
				A_StartSound("weapons/heavyamr/fire", CHAN_WEAPON);
				A_ChangeVelocity(-cos(pitch)*5, 0, sin(pitch)*10,CVF_RELATIVE);
			}
			HAMR BC 1;
			HAMR C 12;
			HAMR B 8;
			HAMR A 4;
			Goto Ready;
		Fire.Empty:
			HAMR A 0 A_StartSound("weapons/shocksmg/empty", CHAN_WEAPON);
			HAMR BBBCCC 2;
			HAMR C 2 A_StartSound("weapons/heavyamr/melee", CHAN_BODY);
			HAMR B 1;
			HAMR A 8 {
				A_CustomPunch(45, true, 0, "GSW_HeavyAMRHitPuff", meleeRange + 20, meleesound: "*fist");
			}
			Goto Ready;
		Flash:
			HAMF A 1 Bright A_AttachLightDef("Flash", "GSW_GunFlash");
			HAMF B 1 Bright;
			HAMF B 0 A_RemoveLight("Flash");
			Stop;
		Hand:
			HAMR Y 1;
			Loop;
		HandStop:
			Stop;
	}
}
class GSW_HeavyAMRBullet : FastProjectile
{
	Default
	{
		+FORCEPAIN
		Radius 6;
		Height 8;
		Speed 200;
		DamageFunction (300);
		Translation "0:223=[98,57,1]:[255,243,21]", "223:255=[255,243,21]:[255,255,254]";
		Obituary "$KILLHEAVYAMR";
		Decal "BulletChip"; // temp
		DamageType "Bullet";
	}
	States
	{
		Spawn:
			GPRJ G -1 Bright;
			Stop;
		Death:
			GPRJ HIJKL 2 Bright;
			Stop;
	}
}
class GSW_HeavyAMRHitPuff : GSW_BulletPuff
{
	Default
	{
		DamageType "Punch";
	}
}